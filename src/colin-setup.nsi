#
# $Id$
#

;
; Placeholders for sed:
;
; __VERSION__      product version
; __ARCHITECTURE__ short architecture name (x86 or x64)
; __ARCHTRIPLET__  architecture triplet for searching locally installed files.
; __PROGRAMFILES__ name program files variable (PROGRAMFILES or PROGRAMFILES64)
; __REGVIEW__      registry view (32 or 64)
; __SRCDIR__       top sourcedir       
; 

; Script generated by the HM NIS Edit Script Wizard.

; HM NIS Edit Wizard helper defines
!define PRODUCT_NAME "Colin (__ARCHITECTURE__)"
!define PRODUCT_VERSION "__VERSION__"
!define PRODUCT_PUBLISHER "Informatik-Buero Dipl.-Ing. Christoph Lechleitner"
#!define PRODUCT_WEB_SITE "http://www.clazzes.org/colin"
!define PRODUCT_DIR_REGKEY "Software\Microsoft\Windows\CurrentVersion\App Paths\Colin.exe"
!define PRODUCT_UNINST_KEY "Software\Microsoft\Windows\CurrentVersion\Uninstall\${PRODUCT_NAME}"
!define PRODUCT_UNINST_ROOT_KEY "HKLM"
!define PRODUCT_PATH_REGKEY "Software\Clazzes.org\Colin"

; MUI 1.67 compatible ------
!include "MUI.nsh"

; MUI Settings
!define MUI_ABORTWARNING
!define MUI_ICON "__SRCDIR__\..\icons\colin.ico"
!define MUI_UNICON "${NSISDIR}\Contrib\Graphics\Icons\modern-uninstall.ico"

; Language Selection Dialog Settings
!define MUI_LANGDLL_REGISTRY_ROOT "${PRODUCT_UNINST_ROOT_KEY}"
!define MUI_LANGDLL_REGISTRY_KEY "${PRODUCT_UNINST_KEY}"
!define MUI_LANGDLL_REGISTRY_VALUENAME "NSIS:Language"

; Welcome page
!insertmacro MUI_PAGE_WELCOME
; License page
!insertmacro MUI_PAGE_LICENSE "__SRCDIR__\..\doc\LICENSE.txt"
; Directory page
!insertmacro MUI_PAGE_DIRECTORY
; Instfiles page
!insertmacro MUI_PAGE_INSTFILES
; Finish page
!define MUI_FINISHPAGE_RUN "$INSTDIR\bin\Colin.exe"
!insertmacro MUI_PAGE_FINISH

; Uninstaller pages
!insertmacro MUI_UNPAGE_INSTFILES

; Language files
!insertmacro MUI_LANGUAGE "English"
!insertmacro MUI_LANGUAGE "German"

!define ARCHITECTURE "__ARCHITECTURE__"
!define PREFIX "/usr/__ARCHTRIPLET__"
!define LOCAL_PREFIX "./install-root/usr/__ARCHTRIPLET__"

; MUI end ------

Name "${PRODUCT_NAME} ${PRODUCT_VERSION}"
OutFile "colin-${PRODUCT_VERSION}-setup-${ARCHITECTURE}.exe"
InstallDir "$__PROGRAMFILES__\Clazzes.org\Colin"
InstallDirRegKey HKLM "${PRODUCT_DIR_REGKEY}" ""
ShowInstDetails show
ShowUnInstDetails show

Var INSTMODE

Function .onInit
  SetRegView __REGVIEW__

  ReadRegStr $0 ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString"
  IfErrors fromscratch 0
  MessageBox MB_OKCANCEL  "There is another version of Colin currently installed. Run Uninstaller $0 now?" IDOK +2 IDCANCEL +1
  Abort "Installation aborted by the user."
  ReadRegStr $INSTDIR HKLM "${PRODUCT_PATH_REGKEY}" "instdir"
  ExecWait '$0 _?=$INSTDIR'
  StrCpy $INSTMODE "update"
  goto goon

fromscratch:
  StrCpy $INSTMODE "install"

goon:

FunctionEnd

Section "main" SEC01

  SetOutPath "$INSTDIR\bin"
  File "${LOCAL_PREFIX}/bin/Colin.exe"
  File "${PREFIX}/bin/libintl-8.dll"
  File "${PREFIX}/bin/libiconv-2.dll"
  File "${PREFIX}/bin/QtGui4.dll"
  File "${PREFIX}/bin/libpng12-0.dll"
  File "${PREFIX}/bin/QtCore4.dll"
  File "${PREFIX}/bin/zlib1.dll"
  File "${PREFIX}/bin/libgcc_s_sjlj-1.dll"

  CreateDirectory "$SMPROGRAMS\Colin"
  CreateShortCut "$SMPROGRAMS\Colin\Colin.lnk" "$INSTDIR\bin\Colin.exe"
  CreateShortCut "$DESKTOP\Colin.lnk" "$INSTDIR\bin\Colin.exe"

  #SetOutPath "$INSTDIR\share\"
  #File "colin.rcc"

  SetOutPath "$INSTDIR\share\icons"
  File "__SRCDIR__/../icons/*.png"
  #File "__SRCDIR__/../media/*.ico"
  SetOutPath "$INSTDIR\share\icons\mouse"
  File "__SRCDIR__/../icons/mouse/*.png"
  SetOutPath "$INSTDIR\share\icons\tooltip"
  File "__SRCDIR__/../icons/tooltip/*.png"
  
  SetOutPath "$INSTDIR\share\translations"
  File "/usr/share/qt4/translations/qt_de.qm"
  File "__SRCDIR__/colin_*.qm"

  SetOutPath "$INSTDIR\doc"
  File "__SRCDIR__/../doc/*.txt"

SectionEnd

Section -AdditionalIcons
  SetOutPath $INSTDIR
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "URL" "${PRODUCT_WEB_SITE}"
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "IconFile" "$INSTDIR\share\symbols\clazzes.org.ico"
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "IconIndex" "0"
  WriteIniStr "$INSTDIR\${PRODUCT_NAME}.url" "InternetShortcut" "IDList" ""
  #CreateShortCut "$SMPROGRAMS\Colin\Colin Website.lnk" "$INSTDIR\${PRODUCT_NAME}.url" "" "$INSTDIR\share\symbols\clazzes.org.ico"
  CreateShortCut "$SMPROGRAMS\Colin\Uninstall Colin.lnk" "$INSTDIR\uninst.exe"
SectionEnd

Section -Post
  WriteUninstaller "$INSTDIR\uninst.exe"
  WriteRegStr HKLM "${PRODUCT_DIR_REGKEY}" "" "$INSTDIR\bin\Colin.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayName" "$(^Name)"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "UninstallString" "$INSTDIR\uninst.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayIcon" "$INSTDIR\bin\Colin.exe"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "DisplayVersion" "${PRODUCT_VERSION}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "URLInfoAbout" "${PRODUCT_WEB_SITE}"
  WriteRegStr ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}" "Publisher" "${PRODUCT_PUBLISHER}"
  WriteRegStr HKLM "${PRODUCT_PATH_REGKEY}" "instdir" "$INSTDIR"
SectionEnd


Function un.onUninstSuccess
  HideWindow
  MessageBox MB_ICONINFORMATION|MB_OK "Colin has been successfully uninstalled."
FunctionEnd

Function un.onInit
  SetRegView __REGVIEW__
  MessageBox MB_ICONQUESTION|MB_YESNO|MB_DEFBUTTON2 "Would you like to uninstall Colin and all its components?" IDYES +2
  Abort
FunctionEnd

Section Uninstall
  Delete "$INSTDIR\${PRODUCT_NAME}.url"
  Delete "$INSTDIR\uninst.exe"
  Delete "$INSTDIR\bin\Colin.exe"
  Delete "$INSTDIR\bin\*.dll"

  Delete "$SMPROGRAMS\Colin\Uninstall Colin.lnk"
  Delete "$SMPROGRAMS\Colin\Colin Website.lnk"
  Delete "$DESKTOP\Colin.lnk"
  Delete "$SMPROGRAMS\Colin\Colin.lnk"

  RMDir "$SMPROGRAMS\Colin"
  
  RMDir "$INSTDIR\bin"
  Delete "$INSTDIR\share\translations\qt*.qm"
  Delete "$INSTDIR\share\translations\colin*.qm"
  RMDir "$INSTDIR\share\translations"
  
  Delete "$INSTDIR\share\icons\mouse\*.png"
  RMDir "$INSTDIR\share\icons\mouse"
  Delete "$INSTDIR\share\icons\tooltip\*.png"
  RMDir "$INSTDIR\share\icons\tooltip"
  Delete "$INSTDIR\share\icons\*.png"
  Delete "$INSTDIR\share\icons\*.ico"
  RMDir "$INSTDIR\share\icons"
  
  RMDir "$INSTDIR\share"
  
  Delete "$INSTDIR\doc\*.txt"
  RMDir "$INSTDIR\doc"
  
  RMDir "$INSTDIR"

  DeleteRegKey ${PRODUCT_UNINST_ROOT_KEY} "${PRODUCT_UNINST_KEY}"
  DeleteRegKey HKLM "${PRODUCT_DIR_REGKEY}"
  DeleteRegKey HKLM "${PRODUCT_PATH_REGKEY}"
  SetAutoClose true
SectionEnd

